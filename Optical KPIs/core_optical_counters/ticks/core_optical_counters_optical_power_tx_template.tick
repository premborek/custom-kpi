//-----------------------------------------------------------------------------
// {{ script_id.value }} - Pulse TICKscript Template
//
// Master template: two_level_threshold-master-template
//
// Description: This alert template detects abnormal measured values using 2 custom
// thresholds and the ability to provide dampening intervals on the thresholds
//
// Threshold for measurement that causes alerts can be set
// in the vars JSON as '<level>_threshold'.
//
// Copyright (c) 2018 by Cisco Systems, Inc.
// All rights reserved.
//-----------------------------------------------------------------------------

//PULSE_RUN_TIME Alert db to store a
var alerts_db string

//PULSE_RUN_TIME InfluxDB Measurement for storing alerts back
var alerts_meas string

//PULSE_RUN_TIME InfluxDB RP for storing alerts back
var alerts_rp string

//PULSE_RUN_TIME KPI Profile id for filtering data stream
var profile_id string

//KPI_INTERNAL url for posting alerts to notifier "http://astack-service:8660/alerts"
var alert_url = 'http://astack-service:8660/alerts'

//KPI_INTERNAL KPI ID of the kpi this TICKscript measures
var kpi_id = 'core_optical_counters'

//KPI_INTERNAL Alert ID for this KPI Alert event
var alert_id = 'core_optical_counters_optical_power_rx'


// ------------------------------------
//KPI_INTERNAL Alert message format string
var level2_message = '{{ index .Tags "level" }}: Transmit Power KPI for interface {{ index .Tags "name" }}: {{ index .Fields "kpi_stream" | printf "%0.2f" }}dBm has crossed threshold: {{ index .Fields "threshold" | printf "%0.2f" }}dBm.'

//KPI_INTERNAL Alert message format string
var level1_message = '{{ index .Tags "level" }}: Transmit Power KPI for interface {{ index .Tags "name" }}: {{ index .Fields "kpi_stream" | printf "%0.2f" }}dBm has crossed threshold: {{ index .Fields "threshold" | printf "%0.2f" }}dBm.'

//KPI_INTERNAL Clear message format string
var clear_message = '{{ index .Tags "level" }}: Transmit Power KPI for interface {{ index .Tags "name" }}: {{ index .Fields "kpi_stream" | printf "%0.2f" }}dBm has gotten back to normal. Normal range: {{ index .Fields "threshold_range" | printf "%s" }}dBm.'


// ------------------------------------


//KPI_INTERNAL Measurement in influxDB to retrieve the data from
var measurement = 'Cisco-IOS-XR-controller-optics-oper:optics-oper/optics-ports/optics-port/optics-info'

//KPI_INTERNAL Optional where filter expression to filter data stream before passing for transformation
var where_filter = lambda: TRUE


// ------------------------------------
// here specify controller numer to monitor
//KPI_THRESHOLDS (Controller port to monitor TX optical power) Select optical controller (i.e. Optics0/0/0/0) to be monitored. Leave '*' to monitor all controllers (no regex possible yet)
var where_port_filter_1 = '*'

// here specify controller numer to monitor
//KPI_THRESHOLDS (Controller port to monitor TX optical power) Select optical controller (i.e. Optics0/0/0/0) to be monitored (no regex possible yet)
var where_port_filter_2 = ' '

// here specify controller numer to monitor
//KPI_THRESHOLDS (Controller port to monitor TX optical power) Select optical controller (i.e. Optics0/0/0/0) to be monitored (no regex possible yet)
var where_port_filter_3 = ' '

//KPI_INTERNAL Lambda expression to reduce raw data to KPI
var where_port_filter = lambda: if(where_port_filter_1 == '*', TRUE , ("name" == where_port_filter_1 OR "name" == where_port_filter_2 OR "name" == where_port_filter_3))
// ------------------------------------


// KPI_INTERNAL filter var to filter data only related to kpi profile id
var where_producer = lambda: strContains("ProfileIDs", profile_id)

//KPI_INTERNAL Optional list of group by dimensions
var groups = [*]
//var groups = ['Producer', 'name']


// ------------------------------------
//KPI_INTERNAL Lambda expression to reduce raw data to KPI
var stream_eval_lambda = lambda: float("lane-data__transmit-power" / 100.0)
// ------------------------------------


// ------------------------------------
//KPI_THRESHOLDS (Level 2 Alert Severity) Severity label of a level 2 alert
var level2_severity = 'CRITICAL'

//KPI_THRESHOLDS (Level 2 Alert Threshold) Generates a level 2 alert when this threshold is exceeded
var level2_threshold = 13.0

//KPI_THRESHOLDS (Level 2 Alert Time) Time that needs to pass after entering the alert state to generate alerts (default 0 seconds)
var level2_time = 0.00

//KPI_THRESHOLDS (Level 1 Alert Severity) Severity label of a level 1 alert
var level1_severity = 'CRITICAL'

//KPI_THRESHOLDS (Level 1 Alert Threshold) Generates a level 1 alert when this threshold is exceeded
var level1_threshold = -23.0
// ------------------------------------


// ------------------------------------
//KPI_INTERNAL string holding information about range to fill 'threshold_range' field in transformantion operation clear_branch
var level1_level2_thresholds = string(level1_threshold) + ' - ' + string(level2_threshold)
// ------------------------------------


//KPI_THRESHOLDS (Level 1 Alert Time) Time that needs to pass after entering the alert state to generate alerts (default 0 seconds)
var level1_time = 0.00

//KPI_THRESHOLDS (Clear Time) Time that needs to pass after entering the clear state to generate an INFO alert (default 0 seconds)
var clear_time = 0.00

//KPI_INTERNAL Unit of time for alert alerts
var time_unit = 1s

//PULSE_RUN_TIME (Downsample Factor) Down sample factor for alert evaluation applied on data stream (Default : 1 , Min : 1 , Max : 10)
var downsample_factor = 1

var data = stream
    |from()
        .measurement(measurement)
        .where(where_producer)
        .where(where_filter)
        .where(where_port_filter)
        .groupBy(groups)
        .quiet()
    |sample(downsample_factor)

var transform = data
    |eval(stream_eval_lambda)             //evaluation/reduction of stream data done here
        .as('kpi_stream') //internal field reference
        .quiet()

var level2_branch = transform
    |stateDuration(lambda: "kpi_stream" > level2_threshold)
        .unit(time_unit)
        .as('duration')
    |default() //insert the thresholds into the data stream
        .tag('kpi_id', kpi_id)
        .tag('level', level2_severity)
        .tag('severity', '2') //2 indicates worst problem
        .field('threshold', level2_threshold)
        .field('alert_src', 'TICK')
        .tag('profile_id', profile_id)

var level1_branch = transform
    |stateDuration(lambda: "kpi_stream" < level1_threshold)
        .unit(time_unit)
        .as('duration')
    |default() //insert the thresholds into the data stream
        .tag('kpi_id', kpi_id)
        .tag('level', level1_severity)
        .tag('severity', '1') //1 indicates danger
        .field('threshold', level1_threshold)
        .field('alert_src', 'TICK')
        .tag('profile_id', profile_id)

var clear_branch = transform
    |stateDuration(lambda: "kpi_stream" >= level1_threshold AND "kpi_stream" <= level2_threshold)
        .unit(time_unit)
        .as('duration')
    |default() //insert the thresholds into the data stream
        .tag('kpi_id', kpi_id)
        .tag('level', 'INFO')
        .tag('severity', '0') //0 indicates safe
        .field('threshold_range', level1_level2_thresholds)
        .field('alert_src', 'TICK')
        .tag('profile_id', profile_id)


level2_branch
    |alert()
         // Critical after input time
         .info(lambda: "duration" >= level2_time)
         .stateChangesOnly()
         .id(alert_id)
         .idField('id')
         .message(level2_message)
         .messageField('msg')
         .noRecoveries()
    |default()
        .tag('state', 'alert')
    |httpPost(alert_url)

level1_branch
    |alert()
         // Critical after input time
         .info(lambda: "duration" >= level1_time)
         .stateChangesOnly()
         .id(alert_id)
         .idField('id')
         .message(level1_message)
         .messageField('msg')
         .noRecoveries()
    |default()
        .tag('state', 'alert')
    |httpPost(alert_url)

clear_branch
    |alert()
        //info on clear after input time
         .info(lambda: "duration" >= clear_time)
         .stateChangesOnly()
         .id(alert_id)
         .idField('id')
         .message(clear_message)
         .messageField('msg')
         .noRecoveries()
    |default()
        .tag('state', 'clear')
    |httpPost(alert_url)
